apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    //noinspection GradleDependency
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "fi.aalto.ssg.opentee.openteeandroid"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        ndk{
            moduleName "nativelibtee"
        }
    }

    publishNonDefault true  // Publish all variants (flavor / build type) of library

    sourceSets.main {
        jni.srcDirs = []    // This prevents the autogeneration of _Android.mk
        jniLibs.srcDirs 'src/main/libs'
    }

    // Task for building JNI
    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    flavorDimensions "mock", "prod"

    productFlavors {
        mock {
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
            dimension "mock"
        }
        full {
            dimension "prod"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable = true
            jniDebuggable = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    //noinspection GradleDynamicVersion
    implementation 'com.android.support:appcompat-v7:+'
    implementation 'com.google.protobuf:protobuf-java:2.6.1'
    implementation project(':otclient')
    implementation project(':opentee')
}
